buildscript {
	repositories {
		google()
		mavenCentral()
		maven {url "https://maven.google.com"}
		maven {url "https://jcenter.bintray.com"}
	}
	
	dependencies {
		classpath 'com.android.tools.build:gradle:7.2.2'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

repositories {
	maven {url "https://maven.google.com"}
	maven {url "https://jcenter.bintray.com"}
	mavenCentral()
	google()
}

def assetDir = "assets"
def properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
	assetDir = properties.getProperty("asset.dir", "assets")
}


android {
	compileSdkVersion 31
	useLibrary 'org.apache.http.legacy'
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = [assetDir]
			jniLibs {
				srcDir 'libs'
			}
		}

		androidTest.setRoot('tests')
		debug.setRoot('build-types/debug')
		release.setRoot('build-types/release')
	}

	dexOptions {
		preDexLibraries = false
	}

	defaultConfig {
		applicationId "mcore.edu.sample"
		minSdkVersion 26
		targetSdkVersion 31
		versionCode 1
		versionName "1.0.0"
		archivesBaseName = "APP" +"_" + new Date().format("yyyyMMdd_HHmmss")
	}
	packagingOptions {
		exclude "META-INF/LICENSE"
		exclude "META-INF/DEPENDENCIES"
	}	
	lintOptions {
		checkReleaseBuilds false
		abortOnError false
	}
	
	if (KEYSTORE_PATH?.trim()) {
		signingConfigs{
			releaseWithSignedKey {
				storeFile file(KEYSTORE_PATH)
				storePassword KEYSTORE_PW
				keyAlias KEYSTORE_ALIAS
				keyPassword KEYSTORE_ALIAS_PW
			}
		}
	}
	
	buildTypes {
		release {
			//minifyEnabled true
			//proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'morpheus_proguard-project.txt'
			if (KEYSTORE_PATH?.trim()) {
				signingConfig signingConfigs.releaseWithSignedKey
			}
		}
	}
}

//추가
repositories {
    flatDir {
        dirs 'libs', 'mcoreLibs'
    }
}



dependencies {
	implementation fileTree(dir: 'mcoreLibs', include: '*.jar', excludes: ['android-support-v4.jar'])
	implementation fileTree(dir: 'mcoreLibs', include: '*.aar')
	
	
	/**
	* ==========================================
	* 외부라이브러리 적용시  [example.aar file]
	* ==========================================
	* implementation name: 'example-release', ext: 'aar'
	*/
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
}

import org.apache.tools.ant.taskdefs.condition.Os

task  AppRunDebug (type: Exec, dependsOn: 'installDebug') {
	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		commandLine 'cmd', '/c', android.getAdbExe().toString(), 'shell', 'am', 'start', '-n', 'mcore.edu.sample/.Startup'
	}
	else if (Os.isFamily(Os.FAMILY_MAC)) {
		commandLine android.getAdbExe().toString(), "shell", 'am', 'start', '-n', 'mcore.edu.sample/.Startup'
	}
}

